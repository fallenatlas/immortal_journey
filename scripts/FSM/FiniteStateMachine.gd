extends Node
class_name FiniteStateMachine


var states : Dictionary = {}
var current_state : State
@export var initial_state : State

var can_update : bool = true

func _ready():
	for child in get_children():
		if child is State:
			states[child.name.to_lower()] = child
			child.state_transition.connect(change_state)

	if initial_state:
		initial_state.Enter()
		current_state = initial_state
		
		
func change_state(old_state : State, new_state_name : String):
	if old_state != current_state:
		return
		
	var new_state = states.get(new_state_name.to_lower())
	if !new_state:
		return
		
	if current_state:
		current_state.Exit()
	new_state.Enter()
	
	current_state = new_state
	
func _process(delta):
	if not can_update:
		return
	if current_state:
		current_state.Update(delta)

func force_change_state(new_state : String):
	var newState = states.get(new_state.to_lower())
	
	if !newState:
		return
		
	if current_state == newState:
		return
	
	if current_state:
		var exit_callable = Callable(current_state, "Exit")
		exit_callable.call_deferred()
		
	newState.Enter()
	
	current_state = newState
